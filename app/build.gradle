/*
 * Copyright 2015 Hippo Seven
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'com.android.application'

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.hippo.nimingban"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 58
        versionName "1.2.44"
        vectorDrawables.useSupportLibrary = true
        resConfigs "zh", "zh-rCN", "zh-rHK", "zh-rTW"
        ndk {
            abiFilters "armeabi", "armeabi-v7a", "x86"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            java.srcDirs += 'src/main/java-gen'
        }
    }

    signingConfigs {
        debug {
            if (project.rootProject.file("local.properties").exists()) {
                Properties properties = new Properties()
                properties.load(project.rootProject.file("local.properties").newDataInputStream())
                def store_file = properties.getProperty("storeFile")
                def store_password = properties.getProperty("storePassword")
                def key_alias = properties.getProperty("keyAlias")
                def key_password = properties.getProperty("keyPassword")

                storeFile file(store_file)
                storePassword store_password
                keyAlias key_alias
                keyPassword key_password
                v1SigningEnabled true
                v2SigningEnabled true
            }
        }
    }
}

task copyNotice(type: Copy) {
    from '../NOTICE'
    into './src/main/assets'
    finalizedBy ':daogenerator:executeDaoGenerator'
}

tasks.withType(AbstractCompile) {
    compileTask -> compileTask.dependsOn copyNotice
}

clean {
    delete file('src/main/assets/NOTICE').absolutePath
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':beerbelly')
    implementation project(':conaco')
    implementation project(':easyrecyclerview')
    implementation project(':photoview')
    implementation project(':yorozuya')
    implementation 'com.alibaba:fastjson:1.1.52.android'
    implementation 'com.dlazaro66.qrcodereaderview:qrcodereaderview:2.0.3'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.github.seven332:advrecyclerview:0.10.0'
    implementation 'com.github.seven332:drawerLayout:0.1.0'
    implementation 'com.github.seven332:image:877cb27e729db592f7f238838583ca74202baa67'
    implementation 'com.github.seven332:refreshlayout:0.1.0'
    implementation 'com.github.seven332:ripple:0.1.1'
    implementation 'com.github.seven332:swipeback:0.1.1'
    implementation 'com.github.seven332:unifile:0.1.4'
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:5.1.6'
    implementation 'org.ccil.cowan.tagsoup:tagsoup:1.2.1'
    implementation 'org.greenrobot:greendao:2.2.1'
    implementation 'org.jsoup:jsoup:1.11.3'
    testImplementation 'junit:junit:4.12'
}
